# Maintainer: dodotronix <dodotronix@gmail.com>
#
_gitrepo='Adafruit_CircuitPython_Typing'
pkgname='python-adafruit-circuitpython-typing-git'
pkgver=1.11.0.r0.g1bad8f5
pkgrel=1
pkgdesc="Definitions not in the standard typing module that are needed for type annotation of CircuitPython code"
arch=('any')
url="https://github.com/adafruit/Adafruit_CircuitPython_Typing"
license=('GPL3')
makedepends=('git' 'python' 'python-build' 'python-installer' 'python-wheel')
depends=('python')
conflicts=('python-adafruit-circuitpython-typing')
provides=("python-adafruit-circuitpython-typing-git=$pkgver")
source=("git+https://github.com/adafruit/${_gitrepo}.git")
sha384sums=('SKIP')

pkgver() {
    cd "${srcdir}/${_gitrepo}"
    git describe --tags --long | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
    cd "${srcdir}/${_gitrepo}"
    python -m build --wheel --no-isolation
}

package() {
    cd "$srcdir/${_gitrepo}"
    python -m installer --destdir="$pkgdir" dist/*.whl
    install -vDm644 -t "$pkgdir/usr/share/license/$pkgname" LICENSE
}
